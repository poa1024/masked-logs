plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.jvm' version '1.5.32'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.32'
}

group = "io.github.poa1024"
version '1.0.1'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}
dependencies {
    compileOnly group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.0'
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'

    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.5.32'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.23.1'
    testImplementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.0'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'masked-logs'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Masked logs'
                description = 'Library for masking sensitive data in logs'
                url = 'https://github.com/poa1024/masked-logs'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'poa1024'
                        name = 'Oleg Perekhod'
                        email = 'poa1024@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git@github.com:poa1024/masked-logs.git'
                    developerConnection = 'scm:git:git@github.com:poa1024/masked-logs.git'
                    url = 'https://github.com/poa1024/masked-logs'
                }

                repositories {
                    maven {
                        name = "ossrh"
                        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                        credentials(PasswordCredentials)
                    }
                }

            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}